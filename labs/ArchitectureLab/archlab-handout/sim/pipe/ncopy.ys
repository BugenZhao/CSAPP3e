#/* $begin ncopy-ys */
##################################################################
# ncopy.ys - Copy a src block of len words to dst.
# Return the number of positive words (>0) contained in src.
#
# Include your name and ID here.
#
# Describe how and why you modified the baseline code.
#
##################################################################
# Do not modify this portion
# Function prologue.
# %rdi = src, %rsi = dst, %rdx = len
ncopy:

##################################################################
# You can modify this portion

#	xorq 	%rax, %rax		# count = 0;
	iaddq	$-8, %rdx
	jg		loop0			# len >= 2

r_test:
	xorq	%r8, %r8
	iaddq	$4, %rdx		# [-8,0] -> [-4,4]	(+4)
	jl		r_test_l
	jg		r_test_r
	je		r4

r_test_l:
	iaddq	$2, %rdx		# [-4,-1] -> [-2,1]	(+2)
	je		r2
	jg		r3
	iaddq	$2, %rdx		# [-2,-1] -> [0,1]	(+0)
	jg		r1
	ret

r_test_r:
	iaddq	$-2, %rdx		# [1,4] -> [-1,2]	(+6)
	je		r6
	jl		r5
	iaddq	$-2, %rdx		# [1,2] -> [-1,0]	(+8)
	jl		r7

r8:
	mrmovq	56(%rdi), %r8
r8b:
	rmmovq	%r8, 56(%rsi)
r7:
	andq	%r8, %r8
	mrmovq	48(%rdi), %r8
	jle		r7b				# 56(%rdi)
	iaddq	$1, %rax
r7b:
	rmmovq	%r8, 48(%rsi)
r6:
	andq	%r8, %r8
	mrmovq	40(%rdi), %r8
	jle		r6b
	iaddq	$1, %rax
r6b:
	rmmovq	%r8, 40(%rsi)
r5:
	andq	%r8, %r8
	mrmovq	32(%rdi), %r8
	jle		r5b
	iaddq	$1, %rax
r5b:
	rmmovq	%r8, 32(%rsi)
r4:
	andq	%r8, %r8
	mrmovq	24(%rdi), %r8
	jle		r4b
	iaddq	$1, %rax
r4b:
	rmmovq	%r8, 24(%rsi)
r3:
	andq	%r8, %r8
	mrmovq	16(%rdi), %r8
	jle		r3b
	iaddq	$1, %rax
r3b:
	rmmovq	%r8, 16(%rsi)
r2:
	andq	%r8, %r8
	mrmovq	8(%rdi), %r8
	jle		r2b
	iaddq	$1, %rax
r2b:
	rmmovq	%r8, 8(%rsi)
r1:
	andq	%r8, %r8
	mrmovq	(%rdi), %r8
	jle		r1b
	iaddq	$1, %rax
r1b:
	rmmovq	%r8, (%rsi)
	andq	%r8, %r8
r1c:
	jle		Done
	iaddq	$1, %rax
	ret



loop0:
	mrmovq	(%rdi), %r8
	iaddq	$72, %rdi
	rmmovq	%r8, (%rsi)
	andq	%r8, %r8
	jle		loop1
	iaddq	$1, %rax
loop1:
	mrmovq	-64(%rdi), %r8
	iaddq	$72, %rsi
	rmmovq	%r8, -64(%rsi)
	andq	%r8, %r8
	jle 	loop2
	iaddq	$1, %rax
loop2:
	mrmovq	-56(%rdi), %r8
	mrmovq	-48(%rdi), %r9
	rmmovq	%r8, -56(%rsi)
	andq	%r8, %r8
	jle 	loop3
	iaddq	$1, %rax
loop3:
	mrmovq	-40(%rdi), %r8
	rmmovq	%r9, -48(%rsi)
	andq	%r9, %r9
	jle 	loop4
	iaddq	$1, %rax
loop4:
	mrmovq	-32(%rdi), %r9
	rmmovq	%r8, -40(%rsi)
	andq	%r8, %r8
	jle 	loop5
	iaddq	$1, %rax
loop5:
	mrmovq	-24(%rdi), %r8
	rmmovq	%r9, -32(%rsi)
	andq	%r9, %r9
	jle 	loop6
	iaddq	$1, %rax
loop6:
	mrmovq	-16(%rdi), %r9
	rmmovq	%r8, -24(%rsi)
	andq	%r8, %r8
	jle 	loop7
	iaddq	$1, %rax
loop7:
	mrmovq	-8(%rdi), %r8
	rmmovq	%r9, -16(%rsi)
	andq	%r9, %r9
	jle 	loop8
	iaddq	$1, %rax
loop8:
	rmmovq	%r8, -8(%rsi)
	andq	%r8, %r8
	jle		loop_end
	iaddq	$1, %rax

loop_end:
	iaddq	$-9, %rdx
	jg		loop0
	jmp		r_test


##################################################################
# Do not modify the following section of code
# Function epilogue.
Done:
	ret
##################################################################
# Keep the following label at the end of your function
End:
#/* $end ncopy-ys */
